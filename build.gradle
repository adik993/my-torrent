buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.franzbecker:gradle-lombok:${lombokPluginVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementPluginVersion}"
        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.3"
    }
}

allprojects {
    apply plugin: "idea"
    apply plugin: "project-report"
    apply plugin: "com.dorongold.task-tree"

    group = "com.adik993"
    version = "1.0-SNAPSHOT"
}

subprojects {
    apply plugin: 'maven'
    apply plugin: "java"
    apply plugin: "io.franzbecker.gradle-lombok"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    lombok {
        version = "${lombokVersion}"
        sha256 = ""
    }

    clean {
        doFirst {
            delete "${projectDir}/out"
        }
    }

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/java/main/")
            testOutputDir = file("$buildDir/classes/java/test/")
        }
    }

    build.dependsOn install

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }

        dependencies {
            //noinspection GroovyAssignabilityCheck
            dependency "com.adik993:tpb-client:${tpbClientVersion}"
        }
    }
}
