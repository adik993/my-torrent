buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.3"
    }
}

allprojects {
    apply plugin: "idea"
    apply plugin: "project-report"
    apply plugin: "com.dorongold.task-tree"

    group = "com.adik993"
    version = "1.1-SNAPSHOT"
}

subprojects {
    apply plugin: 'maven'
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    clean {
        doFirst {
            delete "${projectDir}/out"
        }
    }

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/java/main/")
            testOutputDir = file("$buildDir/classes/java/test/")
        }
    }

    build.dependsOn install

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }

        dependencies {
            dependency "com.adik993:tpb-client:${tpbClientVersion}"
            dependency "io.reactivex.rxjava2:rxjava:${rxjavaVersion}"
            dependency "org.spockframework:spock-core:${spockVersion}"
            dependency "org.spockframework:spock-spring:${spockVersion}"
        }
    }

    dependencies {
        annotationProcessor('org.projectlombok:lombok')
        compileOnly('org.projectlombok:lombok')
    }
}
